if(${PRIVMX_BUILD_DEBUG} MATCHES "FULL")
    set(PRIVMX_USE_DEBUG ON)
    set(PRIVMX_DEBUG_TIME ON)
    set(PRIVMX_DEBUG_OUTPUT_TIMESTAMP_ON_START ON)
elseif(PRIVMX_BUILD_DEBUG)
    set(PRIVMX_USE_DEBUG ON)
    set(PRIVMX_DEBUG_TIME OFF)
    set(PRIVMX_DEBUG_OUTPUT_TIMESTAMP_ON_START OFF)
endif()

if(PRIVMX_BUILD_LOGGER)
    set(PRIVMX_ENABLE_LOGGER ON)
endif()

message(STATUS "Enable utils debug - PRIVMX_BUILD_DEBUG=${PRIVMX_BUILD_DEBUG}")
set(INCLUDE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}/include ${CMAKE_CURRENT_BINARY_DIR}/include)
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/include/privmx/utils/DebugConfig.hpp.in ${CMAKE_CURRENT_BINARY_DIR}/include/privmx/utils/DebugConfig.hpp @ONLY)
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/include/privmx/utils/DebugConfig.hpp DESTINATION include/privmx/utils)
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/include/privmx/utils/logger/Config.hpp.in ${CMAKE_CURRENT_BINARY_DIR}/include/privmx/utils/logger/Config.hpp)
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/include/privmx/utils/logger/Config.hpp DESTINATION include/privmx/utils/logger)

file(GLOB_RECURSE SOURCES ${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp ${CMAKE_CURRENT_SOURCE_DIR}/include/*.hpp)
add_library(privmxutils OBJECT ${SOURCES})
target_include_directories(privmxutils PUBLIC ${INCLUDE_DIRS})
target_compile_options(privmxutils PRIVATE -fPIC -Wall -Wextra -Wno-narrowing)
target_link_libraries(privmxutils PUBLIC Poco::Foundation Poco::JSON Pson)
if(BUILD_SHARED_LIBS)
    set_target_properties(privmxutils PROPERTIES POSITION_INDEPENDENT_CODE True)
endif()
if(PRIVMX_INSTALL_PRIVATE_HEADERS)
    install(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/include/privmx DESTINATION include)
endif()
if(PRIVMX_WERROR)
    target_compile_options(privmxutils PRIVATE -Werror)
endif()

if(PRIVMX_ENABLE_TESTS)
    include(FindGTest)
    include(GoogleTest)
    enable_testing()
    find_package(GTest REQUIRED)
    file(GLOB_RECURSE TESTS_SOURCES ${CMAKE_CURRENT_SOURCE_DIR}/test/*.cpp)
    add_executable(privmxutils_test ${TESTS_SOURCES})
    target_link_libraries(privmxutils_test PUBLIC Poco::Foundation Poco::JSON Pson privmxutils GTest::GTest GTest::Main)
    gtest_add_tests(TARGET privmxutils_test)
endif()
